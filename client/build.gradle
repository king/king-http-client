// Copyright (C) king.com Ltd 2015
// https://github.com/king/king-http-client
// Author: Magnus Gustafsson
// License: Apache 2.0, https://raw.github.com/king/king-http-client/LICENSE-APACHE

buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.11"
	}
}


apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: "net.ltgt.errorprone"


group 'com.king.king-http-client'
version '3.0.0'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

archivesBaseName = 'king-http-client'


dependencies {
	compile 'org.slf4j:slf4j-api:1.7.7'

	compile 'io.netty:netty-all:4.1.15.Final'


	testCompile 'junit:junit:4.11'
	testCompile 'se.mockachino:mockachino:0.6.2'

}


tasks.withType(FindBugs) {
	reports {
		xml.enabled = false
		html.enabled = true
	}
}

task generateJavadoc(type: Javadoc) {
	source = sourceSets.main.allJava
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
	sign configurations.archives
}


if (properties.containsKey('ossrhUsername')) {

	uploadArchives {
		repositories {
			mavenDeployer {
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}

				snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}

				pom.project {
					name 'King Http Client'
					packaging 'jar'
					// optionally artifactId can be defined here
					description 'A asyncronous http client built ontop of netty.'
					url 'https://github.com/king/king-http-client'

					scm {
						connection 'scm:git:https://github.com/king/king-http-client.git'
						developerConnection 'scm:svn:https://github.com/king/king-http-client.git'
						url 'https://github.com/king/king-http-client'
					}

					licenses {
						license {
							name 'The Apache License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						}
					}

					developers {
						developer {
							id 'magnus-gustafsson'
							name 'Magnus Gustafsson'
							email 'magnus.gustafsson@king.com'
						}
					}
				}
			}
		}
	}

}

tasks.withType(JavaCompile) {
	options.compilerArgs += ['-Xep:FutureReturnValueIgnored:OFF' ]
}
